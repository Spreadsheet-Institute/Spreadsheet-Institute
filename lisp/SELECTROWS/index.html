<html>
    <head>
        <title>Spreadsheet Lisp: SELECTROWS</title>
        <link rel="stylesheet" href="/static/shared.css">
        <link rel="shortcut icon" type="image/x-icon" href="/static/favicon.ico">
    </head>
    <body>
        <br>
        <h1 class="centered">S.<a href="/">I</a>/<a href="/lisp">Lisp</a>/</h1>
        <p class="centered"><a href="../SELECTROWS/">SELECTROWS</a></p>
        <br>
        <hr>
        <br>
        <pre>
=<a href="../SELECTROWS/">SELECTROWS</a>(<span class="variable">A1:Z100</span>, <span class="constant">"Salary > 100000, Department = IT"</span>)
-> A1:Z10
        </pre>
        <br>
        <hr>
        <br>
        <pre>
DEFINE(
    SELECTROWS,
    LAMBDA(
        range,
        [row_conditions],
        IFOMITTED(
            row_conditions,
            range,
            LET(
                column_names,
                    FIRSTROW(range),
                condition_strings,
                    TRIMSPLIT(
                        row_conditions,
                        ",",
                        YES),
                condition_count,
                    COUNTA(condition_strings),
                condition_operators,
                    EXTRACTOPERATORS(condition_strings),
                condition_columns,
                    MAKEARRAY(
                        condition_count,
                        1,
                        LAMBDA(
                            row,
                            _col,
                            LET(
                                condition,
                                    INDEX(
                                        condition_strings,
                                        row,
                                        1),
                                operator,
                                    INDEX(
                                        condition_operators,
                                        row,
                                        1),
                                TRIM(
                                    TEXTBEFORE(
                                        condition,
                                        operator))))),
                condition_criteria,
                    MAKEARRAY(
                        condition_count,
                        1,
                        LAMBDA(
                            row,
                            _col,
                            LET(
                                condition,
                                    INDEX(
                                        condition_strings,
                                        row,
                                        1),
                                operator,
                                    INDEX(
                                        condition_operators,
                                        row,
                                        1),
                                result,
                                    TRIM(
                                        TEXTAFTER(
                                            condition,
                                            operator)),
                                IFERROR(
                                    NUMBERVALUE(result),
                                    result)))),
                filter_lookup_table,
                    HSTACK(
                        condition_operators,
                        condition_columns,
                        condition_criteria),
                VSTACK(
                    FIRSTROW(range),
                    SIEVE(
                        TABLEDATA(range),
                        filter_lookup_table))))))
        </pre>
        <br>
        <hr>
        <br>
        <p class="centered">Help us <a target="_blank" href="https://github.com/Spreadsheet-Institute/Spreadsheet-Institute/blob/main/lisp/SELECTROWS/index.html">optimize this function</a></p>
        <br>
    </body>
</html>